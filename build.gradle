plugins {
    id 'java'
    id 'org.springframework.boot' version '2.2.6.RELEASE'
    id 'pl.allegro.tech.build.axion-release' version '1.11.0'
}

apply plugin: 'io.spring.dependency-management'

group = 'co.insecurity.example.springboot.oauth2'
scmVersion {
    tag {
        prefix = 'v'
        versionSeparator = ''
    }
    hooks {
        pre 'fileUpdate', [file: 'README.md', pattern: {v, c -> /\b$v\b/}, replacement: {v, c -> /$v/}]
        pre 'commit'
    }
}
project.version = scmVersion.version
logger.info("version: ${project.version}")

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

repositories {
    mavenCentral()
}

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}


dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'

    implementation 'org.webjars:webjars-locator-core'
    implementation 'org.webjars:jquery:3.4.1'
    implementation 'org.webjars:bootstrap:4.4.1-1'
    implementation 'org.webjars:js-cookie:2.2.1'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'

    developmentOnly 'org.springframework.boot:spring-boot-starter-actuator'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
}
